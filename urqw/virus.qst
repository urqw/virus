:main
music="1.ogg"
kill=0
boss=0
open=0
sector=0
room1=0
room2=0
virus=0
sara=0
door2=0
zomb5=0
mon=0
door1=0
box1=0
mon=0
pla=0
plant=0
fa=*
sa=*
ta=*
kod1=0
instr style_nonactivetext=Вирус
key1=0
ink1=0
secb=1
seca=1
secc=0
fight=0
pods=0
instr style_formpicture=55.jpg
cor=0
med1=0
bd=0
instr ba=*
instr aa=*
instr ca=*
zomb1=0
light=0
pln                          Вирус
pln                    made by Terracon
pln                e- mail:danil1@mailru.com
pln               Home Page:terracon.narod.ru
btn new,Новая игра
btn about,Об игре
btn autor,Авторы
end

:autor
pln Всё делал Terracon. Мой сайт - terracon.narod.ru, а е-майл - danil1@mailru.com. 
btn main,Ok
end

:about
pln Сюжет игры частично взят из всех частей Resident Evil (плюс одноимённый фильм) , а частично придуман мною самим.
btn main,Ok
end

:new
inv+ 100,Здоровья
pln Вы шагаете по коридору какой-то лаборатории. В памяти сплошной туман и ничего кроме своего имени вы не помните. И это имя - Ray. Повсюду мощные прожекторы, а дверь через которую вы, видимо ранее зашли закрыта.
btn 1,Идти вперёд
end

:1
light=0
pln Вы подходите к концу туннеля. Тут стоит дверь с электронным замком. На мини-мониторе высвечена надпись "Введите ваше имя". На часть кнопок попала кислота и они не работают. Ну, что же, будем надеяться, что из оставшихся можно будет сложить подходящий пароль.
btn 3,Ввести имя
end

:3
bd=bd+1
instr style_formmaximumheight=9000
pln #%aa$#%ba$#%ca$
btn 3.01,A
btn 3.02,B
btn 3.03,C
btn 3.04,D
btn 3.05,E
btn 3.06,F
btn 3.07,G
btn 3.08,H
btn 3.09,I
btn 3.11,K
btn 3.13,M
btn 3.14,N
btn 3.15,O
btn 3.16,P
btn 3.18,R
btn 3.19,S
btn 3.2,T
btn 3.22,V
btn 3.23,W
btn 3.25,Y
btn 3.26,Z
btn 2,Ok
end

:3.01
if bd=1 then instr aa=A
if bd=2 then instr ba=A & cor=cor+1
if bd=3 then instr ca=A
goto 3
end

:3.03
if bd=1 then instr aa=C
if bd=2 then instr ba=C
if bd=3 then instr ca=C
goto 3
end

:3.02
if bd=1 then instr aa=B 
if bd=2 then instr ba=B
if bd=3 then instr ca=B
goto 3
end

:3.04
if bd=1 then instr aa=D 
if bd=2 then instr ba=D
if bd=3 then instr ca=D
goto 3
end

:3.05
if bd=1 then instr aa=E
if bd=2 then instr ba=E
if bd=3 then instr ca=E
goto 3
end

:3.06
if bd=1 then instr aa=F 
if bd=2 then instr ba=F
if bd=3 then instr ca=F
goto 3
end

:3.07
if bd=1 then instr aa=G 
if bd=2 then instr ba=G
if bd=3 then instr ca=G
goto 3
end

:3.08
if bd=1 then instr aa=H 
if bd=2 then instr ba=H
if bd=3 then instr ca=H
goto 3
end

:3.09
if bd=1 then instr aa=I 
if bd=2 then instr ba=I
if bd=3 then instr ca=I
goto 3
end

:3.11
if bd=1 then instr aa=K 
if bd=2 then instr ba=K
if bd=3 then instr ca=K
goto 3
end

:3.13
if bd=1 then instr aa=M 
if bd=2 then instr ba=M
if bd=3 then instr ca=M
goto 3
end

:3.14
if bd=1 then instr aa=N 
if bd=2 then instr ba=N
if bd=3 then instr ca=N
goto 3
end

:3.15
if bd=1 then instr aa=O 
if bd=2 then instr ba=O
if bd=3 then instr ca=O
goto 3
end

:3.16
if bd=1 then instr aa=P 
if bd=2 then instr ba=P
if bd=3 then instr ca=P
goto 3
end

:3.18
if bd=1 then instr aa=R & cor=cor+1
if bd=2 then instr ba=R
if bd=3 then instr ca=R
goto 3
end

:3.19
if bd=1 then instr aa=S 
if bd=2 then instr ba=S
if bd=3 then instr ca=S
goto 3
end

:3.2
if bd=1 then instr aa=T 
if bd=2 then instr ba=T
if bd=3 then instr ca=T
goto 3
end

:3.22
if bd=1 then instr aa=V
if bd=3 then instr ba=V
if bd=3 then instr ca=V
goto 3
end

:3.23
if bd=1 then instr aa=W 
if bd=2 then instr ba=W
if bd=3 then instr ca=W
goto 3
end

:3.25
if bd=1 then instr aa=Y 
if bd=2 then instr ba=Y
if bd=3 then instr ca=Y & cor=cor+1
goto 3
end

:3.26
if bd=1 then instr aa=Z 
if bd=2 then instr ba=Z
if bd=3 then instr ca=Z
goto 3
end

:4
pln Пароль верен
pause 1500
cls
pln Доступ разрешён

:4.1
pln Вы вошли в следующее помещение. Здесь явно что-то произошло. Всюду следы человеческой крови, но нет ни одного мёртвого тела. Здесь по кому-то стреляли и несколько ламп в дальнем углу просто не горят, там теперь кромешная тьма .Видна дверь, за которой - новая локация. Только теперь вы замечаете, что одеты в униформу. Бронежилет на теле, синий берет на голове. Похоже вы из какой-то спец - службы . Тем не менее никакого оружия, кроме ножа у себя вы не находите.

if not Армейский нож then inv+ Армейский нож
if not Бронежилет then inv+ 100,Бронежилет
if not Фонарик then inv+ Фонарик
btn 5,Открыть дверь
if light=0 then btn 6,Включить фонарь
if light=1 then btn 7,Обыскать дальний угол
end

:7
if Карта C then pln Здесь больше ничего нет, кроме крови и следов пуль. & goto 4.1
inv+ Карта C
inv+ План
pln Вы находите карточку с линией штрих - кода и план здания.
goto 4.1
end

:Use_План
imagetype=6
image=54
end

:6
pln Вы зажгли наплечный фонарь.
light=1
goto 4.1
end

:5
kill=0
if not Карта C then pln Высвечивается надпись "Нужен пропуск". & goto 4.1
pln Вы проходите в следующее помещение. Здесь, опираясь об угол стены, лежит какой-то человек. Он суёт вам в руки пистолет и со словами "Возьми и убей меня" умирает.
inv+ Пистолет Глок 17
inv+ 15, Патронов
pln       ВЫ ПОЛУЧИЛИ ПИСТОЛЕТ ГЛОК 17 И ОБОЙМУ К НЕМУ
pause 4000 
GOTO 8
end

:8
cls
pln Вы проверили его пульс - так и есть, теперь он мёртв.
btn 9,Выстрелить ему в голову
btn 10,Осмотреться
end

:9
kill=1
inv- Патронов
goto 10

:10
if mon=2 then pln Вы пошли навстречу своей смерти - монстр одним ударом разрубил вас пополам. & goto 8f
if plant=1 and zomb4=0 then pln В этом помещении какой-то человек отстреливался от натиска пятерых зомби. Один зомби добрался до него и вцепился ему в шею. Вы быстро подбежали и всадили этому зомби нож прямо в череп. Похоже, что вы прикончили его, но внимание остальных четверых его приятелей теперь полностью обращено на вас. & fight=4 & goto f4
if plant=1 then pln Раненый человек пришёл в себя, поднялся, осмотрел мёртвых зомби, что лежали на полу, затем посмотрел на вас. На его лице появилась какая-то злая улыбка и он сказал "Мдаа, не ждал от тебя помощи, Рэй. Жаль, что прийдётся отплатить тебе обратным!". Он достал свой пистолет и три раза выстрелил в цистерну с монстром. Стекло не выдержало большого давления и разлетелось вдребезги. На усеянный осколками пол упала двухметровая туша. Тот мерзавец убежал в сектор А, закрыв за собой дверь. А монстр начал приходить в себя и медленно подниматься на ноги. Нужно делать ноги! & mon=1 
if kill=0 then pln Внезапно мертвец сзади вас встал и набросился на вас. К счастью пострадал лишь бронежилет и вы, оттолкнув его, прикончили беднягу одним выстрелом в голову. Когда зомби упал, из его кармана вывалился какой-то ключ. & key1=1 & inv+ Ключ от подсобки & inv- 20,Бронежилет & inv- Патронов & kill=1 & pln ВЫ ПОЛУЧИЛИ КЛЮЧ
if plant=0 then pln Вы вошли в следующее помещение. Стены, как и везде, здесь все в пятнах крови и следах от пуль. Также здесь стоят три большие стеклянные цистерны. Из них не разбита только одна и в этой цистерне, в ней... монстр, весь покрытый чешуёй, с когтями длиной 30 см... Он находится в растворе, видимо поддерживающем его сон. Не хочется даже думать о тех несчастных, что оказались рядом, когда две оставшиеся цистерны разбились. Однако при всём этом нет ни одного трупа... Отсюда есть двери в сектор A и в сектор B.
if seca=0 then pln Вы снова в этом помещении. & boss=boss+1
btn 11, В сектор A
btn 12, В сектор B
end

:11
if plant=1 then pln Монстр преградил вам путь и одним ударом своей огромной лапищи с остыми и длинными когтями перерезал вас пополам. & goto 8f
if seca=1 then pln Дверь заперта. & goto 10
goto 77
end

:12
if secb=0 then pln Дверь сильно повреждена когтями монстра, но всё ещё закрыта. & goto 10
if mon>0 then mon=mon+1
if mon>2 then pln Монстр добрался до вас и разорвал вас на куски. & goto 8f
if plant=1 and secc=1 then pln Растения-мутанты добрались до вас и разорвали в клочья. & goto 8f
if plant=1 then pln На мониторе одного из компьютеров горит надпись " К вам доставлена почта " & btn 12.1,Проверить почту
p Двери перед вами автоматически открылись и вы вошли в сектор B .Здесь повсюду вонь разлагающейся плоти. Всё помещение заставлено компьютерами и какой-то техникой, впрочем половина из них поломана из-за огнестрельных выстрелов.
if zomb1=0 then p Вроде бы никого здесь нет, однако помещение продолжается за углом, откуда слышаться какие-то звуки.
btn 13,Идти за угол
btn 10,Выйти
end

:12.1
pln Высветилась надпись " Просим извинения за задержку, из-за проблем на веб-узле. Задержка сообщения : 2 часа. ".
pln Кому : капитану Рэю Джонсону
pln От кого : штаб-квартира V.A.M.
pln "Капитан, мы получили сигнал SOS с вашего сектора. Мы не можем рисковать людьми, чтобы выслать к вам подмогу. В связи с тем, что большая часть лаборатории покрыта радиационными растениями и сильно повреждена "Растением 100", а зачистка лаборатории от зомби и других мутантов является рискованной, мы сочли нужным задействовать ядерные ракеты, которые полностью уничтожат всю лабораторию ровно через три часа. За это время вы и весь незаражённый персонал должны покинуть лабораторию и улететь на двух вертолётах на взлётной площадке."
pln Значит остался всего час до того, как всё здесь будет уничтожено.
btn 12,Отойти от компьютера 
end

:13
if mon>0 and secb=0 then goto 36
if mon>0 then mon=mon+1
if mon>3 then pln Монстр добрался до вас и разрубил одним ударом своей лапищи. & goto 8f
if mon>0 then pln Монстр вот-вот зайдёт в сектор В, а тогда спасения уже точно не будет.
if plant=1 then pla=pla+1
if plant=1 and pla>4 and secc=1 then pln Растения-мутанты добрались до вас и разорвали в клочья. & goto 8f
if plant=1 and secc=0 then pln Растения-мутанты пытаются прожечь кислотой титановую дверь, но похоже им это не удаётся. 
if zomb1=0 then pln Вы увидели двух зомби, пожирающих мёртвого человека. Похоже, они заметили вас и теперь придётся отстреливаться. & goto f1
pln В этом помещении находится компьютер, управляющий всеми электронными замками. Также здесь есть две двери - в подсобку и в сектор C.
btn 14,Включить компьютер
btn 15,Зайти в подсобку
btn 12,Идти к другому концу помещения
btn 20,Идти в сектор C
end

:20
if secc=0 then pln Дверь закрыта электронным замком. & goto 13
if plant=1 then pla=pla+1
if plant=1 and pla>3 then pln Растения-мутанты добрались до вас и разорвали вас на части. & goto 8f
pln Вы зашли в сектор C. Здесь какая-то лаборатория. Стоит много стеклянных колб, но все они пусты. Похоже, что кто-то в спешке забирал подопытных тварей оттуда. Что бы это могло значить?
if zomb2=0 then pln Внезапно кто-то схватил вас за ногу. Вы опустили свой взгляд и вас чуть не стошнило. Мертвец, лишившийся ног и части живота подползает к вашей ноге в надежде перекусить. Опять битва не на жизнь, а на смерть! & goto f2 
spr=0
p Видна дверь в следующее помещение, а также мертвый служащий персонала в углу.
if pism=0 then pln На полке рядом с вами лежит чей-то дневник.
btn 21,Осмотреть труп
btn 23,Просмотреть дневник
btn 24,В следующее помещение
btn 13,В сектор B
end

:24
if plant=1 then pla=pla+1
if pla>2 then pln Растения-мутанты добрались к вам и убили своей кислотой. & goto 8f
if Огнемёт and mox=0 then btn 24.1,Поджечь мох огнемётом
if spore=0 then pln Как только открылась дверь, в помещение заструился какой-то зелёный туман. Вы почувствовали жжение по всей коже и отошли подальше от двери. Теперь вы заметили, что это не туман, а какие-то крошечные пылинки... споры. Видимо их выпускает какое-то растение, над которым поработали генетики. В любом случае, долго пребывать в следующем помещении явно не стоит. & spore=1 & goto 20
if not Спецкостюм then spr=spr+1
if spr>2 then pln Вы вдохнули и споры попали в ваши лёгкие. Жжение просто невыносимое, ещё чуть-чуть и вы останетесь здесь навсегда. & inv- 10,Здоровья
if mox=0 then p Всё помещение обросло каким-то мхом, который, видимо и выделяет эти споры.
if zomb3=0 then p На полу валяется три трупа, видимо погибших из-за удушья. 
p Рядом с вами - дверь, однако похоже, что на ней кодовый замок. Также есть, судя по всему незапертая дверь в десяти метрах от вас.
if mox=1 then btn 24.2,Открыть камеру хранения
btn 26,К кодовой двери
btn 30,Идти к незапертой двери
btn 20,Идти в сектор C
end

:24.2
if Карта B then pln Внутри ничего нет & goto 24
pln На маленьком экране высветилась надпись "Запасная карта В. Только для персонала. Введите пароль.". Вы ввели своё имя и замок открылся. В ящике была  карта В.
inv+ Карта B
goto 24
end

:24.1
pln Вы подожгли мох в некоторых местах со своего огнемёта. Весь мох мгновенно сгорел и вы увидели встроенную в стену камеру хранения.
mox=1
inv- Топливо
goto 24
end

:26
if plant=1 then pln Растения-мутанты загнали вас в тупик и разорвали в клочья. & goto 8f
if kod1=1 then goto 27
spr=spr+1
if spr>2 then pln Вы вдохнули и споры попали в ваши лёгкие. Жжение просто невыносимое, ещё чуть-чуть и вы останетесь здесь навсегда. & inv- 10,Здоровья
pln "Введите код"
pln #%fa$ #%sa$ #%ta$
btn 26.1,1
btn 26.2,2
btn 26.3,3
btn 26.4,Ok
end

:26.1
if fa=* then fa=1 & goto 26
if sa=* then sa=1 & goto 26
if ta=* then ta=1 & goto 26
end

:26.2
if fa=* then fa=2 & goto 26
if sa=* then sa=2 & goto 26
if ta=* then ta=2 & goto 26
end

:26.3
if fa=* then fa=3 & goto 26
if sa=* then sa=3 & goto 26
if ta=* then ta=3 & goto 26
end

:26.4
if otv=1 and fa=2 and sa=2 and ta=3 then goto 27
if otv=2 and fa=3 and sa=1 and ta=1 then goto 27
if otv=3 and fa=2 and sa=3 and ta=1 then goto 27
pln Пароль не верен
fa=*
sa=*
ta=*
goto 24
end

:27
if kod1=0 then pln "Пароль верен". Вы вошли в следующее помещение.
kod1=1
p Воздух здесь намного чище, из-за мощной вентиляции. Вы отдышались и осмотрели помещение.
spr=0
if not Спецкостюм then p На полу валяется кем-то сброшенный спецкостюм. & btn 28,Одеть спецкостюм
if med2=0 then p В аптечке остался последний спрэй первой помощи. & btn 29,Взять спрэй
btn 24,Выйти из помещения
end

:28
pln Вы отдели на себя спецкостюм. Теперь никакие споры вам не страшны.
inv+ Спецкостюм
goto 27
end

:29
pln Вы взяли спрей первой помощи.
inv+ F.A.S.
med2=1
goto 27
end

:30
if plant=1 then pla=pla+1
if pla>1 then pln Пока вы мешкали, растения добрались до вас и разорвали в клочья & goto 8f
if zomb3=0 then pln Чёрт! Трупы, что лежали на полу оказались зомби! Видимо эти проклятые споры тоже заражены вирусом. Вся кожа у этих зомби стала светло-зелёной, а на шее образовался огромный прозрачный мешок с какой-то жидкостью внутри. Один из зомби сделал глотательное движение шеей и выплюнул всю жижу из подшейного мешка целясь в вас. Хорошо, что он был достаточно далеко и жижа попала лишь на пол. Жижа начала разъедать стальной пол лаборатории! Придётся сражаться с этими уродами! & goto f3
spr=0
pln Вы вошли в следующее помещение. Хотя бы здесь нет никаких мертвецов. Хотя нет, есть один. Прямо перед вами на стуле перед включённым компьютером сидит какой-то человек в спецкостюме с простреленным затылком. Похоже кто-то убил его, когда он работал на компьютере. Значит здесь есть живые люди, которых нужно опасаться. Есть два выхода - в помещение со спорами и в ботанический отдел.
btn 31,Обыскать труп
btn 34,Осмотреть компьютер
btn 35,Идти в ботанический отдел
btn 24,Идти в помещение со спорами
end

:35
if plant=2 then goto 51
if plant=1 then pln Вы пошли навстречу своей смерти. & goto 8f
if Карта B then pln Вы открыли дверь и вошли.
if not Карта B then pln Высвечивается надпись " Нужна карта В ". & goto 30
plant=1
pla=0
pln Вы зашли в ботанический отдел и увидели следующее : Из многочисленных дыр в потолке свисали огромные стебли, питающие "почки". Внутри почек явно что-то созревало. Внезапно вашу руку оплёл хлыст одного из плотоядных растений. Вы не смогли высвободить руку от его хлыста и поэтому просто перерубили его своим ножом. Вы хотели уже сражаться, но вдруг увидели, что на потолке прямо над вами свисают такие же уроды. Их было всего штук десять, а у вас - меньше, чем полкартриджа горючего. У вас не было никаких шансов победить в этой схватке и вы быстро побежали в предыдущее помещение. Дверь здесь закрыть нечем и скоро она автоматически откроется перед растениями. Нужно бежать!
goto 30
end

:51
if zomb7=0 then pln Вы с Сарой зашли в ботанический отдел. Преследовавшие вас растения-мутанты так и остались у входа в сектор В и теперь придётся разобраться с теми, что созрели позже и остались возле "мамочки". & goto f7
pln Вы нашли ваших погибших приятелей и обнаружили у них карточку от аварийного лифта и маленький ключ. & inv+ Карта от лифта & inv+ Маленький ключ
pln Вы находитесь в ботаническом отделе. Отсюда можно пойти к помещению со спорами, обратно в туннель или на в дверь, за которой лестница на второй этаж.
btn 52,К помещению со спорами
btn 53,В туннель
btn 54,На второй этаж
end

:52
pln Вы добрались до помещения со спорами и не заметили, что вас окружила дюжина растений - мутантов. У вас не было никаких шансов... 
goto 8f
end

:53
pln Во время сражения кислота попала на железную дверь и испортила замок. Туда теперь не пройти.
goto 51
end

:54
p Вы поднялись на второй этаж. Вы находитесь в зале с зеркалами. Здесь на столике стоит печатная машинка, есть дверь в следующее помещение и лестница на первый этаж. 
if not План2 then pln К стене приклеена карта второго этажа. & btn 60,Взять карту этажа
btn 61,В следующее помещение
btn 55,Осмотреть столик
btn 56,Спуститься на первый этаж
end

:61
pln Вы вошли в большой зал. И увидели ЕГО... ТО, что было в ботаническом отделе - всего лишь корни и почки, а растения - мутанты - это побеги. Теперь перед вами во всей красе Растение 100. Стебель, толщиной пять метров весь покрыт огромными пастями с длинными зубами, зелёно - жёлтыми глазами и огромными хлыстами. С таким противником не справится даже огнемёт. Вы быстро бежите к следующей двери, но она оказывается заперта. Никакой замочной скважины. Справа от вас на стенах висят мифологические и по - видимому дорогие картины с панелью ввода слова под ними. Эта загадка для открытия двери. Сара кричит тебе "Давай же, я отвлеку растение!".
pln Вы бежите к первой из картин
goto 62 
end

:62
pln Вопрос : Назовите существо с туловищем женщины, змеиным хвостом вместо ног и шестью руками.
btn 63,Грифон
btn 64,Нага
btn 63,Медуза Горгона
btn 63,Мантикора
btn 63,Минотавр
end

:64pln Вопрос : Назовите существо, имеющее тело и голову льва, крылья летучей мыши и хвост скорпиона.
btn 63,Грифон
btn 63,Нага
btn 63,Медуза Горгона
btn 65,Мантикора
btn 63,Минотавр
end

:65
pln Вопрос : Назовите существо, имеющее туловище человека и голову быка.
btn 63,Грифон
btn 63,Нага
btn 63,Медуза Горгона
btn 63,Мантикора
btn 66,Минотавр
end

:66
pln Вопрос : Назовите существо, имеющее тело и голову женщины, у которой вместо ног змеиный хвост, а вместо волос - извивающиеся змеи
btn 63,Грифон
btn 63,Нага
btn 67,Медуза Горгона
btn 63,Мантикора
btn 63,Минотавр
end

:67
pln Вопрос : Назовите существо, имеющее туловище льва, а голову и крылья орла.
btn 68,Грифон
btn 63,Нага
btn 63,Медуза Горгона
btn 63,Мантикора
btn 63,Минотавр
end

:68
pln Нужная вам дверь сама раскрылось и оставалось только вбежать в неё. В оглянулись в поисках Сары. Она маневрировала возле растения, уворачиваясь от его хлыстов. Вы окликнули её и она повернулась. За это врмя один из стеблей со всего размаху ударил Сару в живот. Удар был настолько силён, что она отлетела до самой противоположной стены и потерла сознание. Вы быстро подбежали и взяли её на руки. Добравшись до нужной двери вы накрепко закрыли её изнутри.
pln Вы положили Сару на диван и осмотрели её рану. На месте ушиба кожа стала просто красного цвета. Вы своими глазами видели, как кожа вокруг ушиба становилась такой же красной. Вы присмотрелись и достали несколько маленьких иголочек из кожи Сары. Значит она заражена вирусом. Если немедля не приморозить область заражения спрейем первой помощи, то Сара умрёт или, что ещё хуже превратится в мутанта.
sara=0
if F.A.S. then btn 69,Использовать спрей первой помощи
btn 70,Отойти от Сары
end

:69
pln В раствор спрея входило и мощное противоядие и поэтому кожа быстро приобрела свой обычный цвет и через несколько минут Сара пришла в себя. Ты вкратце объяснил что произошло. "Ты остановил моментальное заражение, но вирус всё равно попал мне в кровь. Нужно найти вакцину, иначе я превращусь в зомби." . Ты взглянул на свои часы "Сара, до взрыва осталось двадцать минут." - "Тогда нужно торопиться !"
sara=1
virus=1
inv- F.A.S.
goto 70
end

:70
kill=0
boss=0
pln Вы находитесь в помещении для важных приёмов. Здесь стоит складная лестница. Есть выход в следующее помещение. Также есть дверь в зал с Растением 100, однако что - то подсказывает вам, что лучше туда не соваться.
btn 72,Осмотреть лестницу
btn 71,Осмотреть помещение
if room2=1 and sara=0 then btn 75,Подойти к Саре
if room2=1 then btn 76,Осмотреть часы
btn 73,Выйти из помещения
end

:73
pln Вы зашли в сектор А и увидели того самого человека, который высвободил на свободу гигантского мутанта. Ни Сара, ни вы не успели поднять оружие, как он уже целился двумя Береттами в ваши головы.
pln -Только без нервов ! А вы ещё живы ! Ха, а я - то думал, что та тварюга убила вас ! Придётся это сделать мне. Но сначала вы кое - что для меня сделаете. Понимаете, дело в том, что люди, которым я собираюсь продать вирус заплатят мне миллионы. Но ни заплатят мне намного больше, если я отдам им ещё и антивирус, вакцину. Чтобы открыть тайник, где лежит вакцина этот проклятый компьютер запрашивает пароль, название вируса. А я, я просто не подумал об этом раньше, не знаю я его. Итак, кто здесь знает это чёртовое название ? 
pln -Я знаю. Пароль : Вероника.
pln -Очень хорошо, я знал, что могу на вас положиться - он ввёл пароль в компьютер. Открылась панель, но внутри был пустой футляр. - Чёрт, зачем я здесь столько проторчал ?! Мдаа ребята, спасибо, что помогли, но похоже, что пришло время нам рас... О, чёрт !
pln Сзади него открылась дверь, откуда вышел тот самый мутант, которого Ричард недавно освободил. Тварь двинулась в его сторону, но Ричард поднял свои пистолеты и начал стрелять в тварь. Он высадил целую обойму, но сквозь толстую чешую до мяса долетело всего пара пуль, которые не остановили тварь. Мутант подошёл к Ричарду и рывком всадил длинные когти в его живот. Изо рта Ричарда потекла тёмная кровь. Ещё рывок - и когти другой лапы чудища в груди. Покончив с Ричардом, тварь покосилась в нашу сторону. Но не успела она сделать и шагу, как остаток обоймы автомата был всажен прямо в голову твари. Из огромной дыры в голове потекла какая - то белая жидкость. Похоже, что это остановило тварь.
if sara=1 then pln -Остановило, но не надолго. Немного тут походим и он снова оживёт. - ответила Сара, как будто прочитав ваши мысли. Нужно торопиться, у нас осталось совсем мало времени.
btn 77,Осмотреться 
end

:77
boss=boss+1
if boss>9 and kill=0 then pln Ну вот, дождались! Кожа мутанта регенерировалась и тепрь вам предстоит нелёгкая схватка & goto f10
seca=0
plant=0
sector=1
pln Вы находитесь в секторе А. Тут находится компьютер. На полу лежат два тела : одно человеческое, а другое - огромного мутанта. Есть три выхода : в сектор с трмя ныне пустыми цистернами, в кабинет для собраний и в неизвестное помещение.
btn 78,Включить компьютер
btn 10,Выйти в сектор с цистернами
btn 79,Идти в кабинет для конференций
btn 80,Идти в следующее помещение
end

:80
p Вы находитесь в небольшом помещении.
if zomb8=0 and boss<10 then pln К вам движутся четыре зомби. & goto f8
pln Вы находитесь в коротком коридоре. Есть три входа : в сектор А, к аварийному лифту и к коридору с кучей зомби.
btn 77,В сектор А
btn 84,К аварийному лифту
btn 46,В коридор с зомби
end

:79
pln На это сейчас нет времени !
goto 77
end

:78
pln Вы включили компьютер и вставили карточку от аварийного лифта. Компьютер просканировал её и высветилась надпись "Доступ открыт".
open=1
goto 77
end

:74
if not Руна then pln Вы пододвинули лестницу к часам, отодвинули часовой диск и нашли внутри тайничок в котором был спрятан камень формы прямоугольника. & inv+ Руна & goto 70
pln В пустом тайнике вы ничего не обнаружили.
goto 70
end

:76
pln Часовой диск почему - то значительно выступает вперёд, как будто является крышечкой какого - то тайника.
room1=1
goto 70
end

:75
pln Вы проверили пульс - Сара мертва. Через несколько часов, когда вирус окончательно закончит своё дело, с этого дивана поднимется уже зомби. Вы взяли её автомат.
if not Автомат М-16 then inv+ Автомат М-16 & inv+ 30,Патронов к автомату
goto 70
end

:72
pln Небольшая лестница. Кто - то специально поставил её здесь. А вот и причина - одна из неоновых ламп прямо над вами не работает.
if room1=1 then btn 74,Двигать лестницу к часам
btn 70,Отойти от лестницы
end

:71
room2=1
p Небольшая комната для приёмов. Именно здесь боссы фирм обсуждают важные контракты.
if sara=0 then p На кожаном диване лежит Сара.
pln Высоко на стене висят красивые старинные часы.
goto 70
end

:63
pln Высветилась надпись "Ответ неверен" и все вводные панели закрылись защитными планками. Вы оглянулись и увидели как два хлыста растения схватили Сару и с хрустом переломили её позвоночник. Вы попытались убежать в зал с зеркалами, но хищные хлысты достали и вас и поместили прямо в глотку плотоядному растению.

goto 8f
end

:Use_План2
imagetype=6
image=53
end

:60
inv+ План2
pln Вы взяли карту второго этажа.
goto 54
end

:55
p На столике стоит печатная машинка
if ink3=0 then p и чернильная лента & btn 57,Взять чернильную ленту
pln .
btn 58,Использовать печатную машинку
btn 54,Отойти от столика
end

:57
pln Вы взяли чернильную ленту
inv+ Чернильная лента
ink3=1
goto 55
end

:58
if not Чернильная лента then pln Для записи нужна чернильная лента! & goto 57
pln Вы действительно хотите записаться ?
btn 59,Да
btn 55,Нет
end

:59
pln Вы записали свою игру.
inv- Чернильная лента
save 59
style_fontpicture=55.jpg
style_nonactivetext=Вирус
music="1.ogg"
goto 55
end

:56
pln Сара говорит, что на это нет времени.
goto 54
end

:36
pln Вы закрыли вход в сектор В, однако похоже, что монстра титановая дверь остановит ненадолго. Он со всего размаху бьёт её плечом и скоро выбьет её с корнями. В сектор С дорога закрыта, так как там сейчас дюжина растений мясоедов. Вы в тупике. Но вдруг вы замечаете вентиляционную шахту. Вы выбиваете железную сетку, закрывающую её и быстро ползёте вперёд. Для монстра она слишком узкая и поэтому можно не волноваться о том, что монстр полезет за вами. Наконец вы вылезаете в каком-то помещении. Здесь чей-то кабинет. & goto 37
end

:37
if sara=1 and zomb9=0 then pln Сюда уже забралось несколько зомби и теперь придётся отстреливаться. & goto f9
zomb5=0
pln Вы в чьём-то рабочем кабинете. Здесь стоит письменный стол, на стене висит какая-то картина, есть выход из кабинета.
if mon=1 then btn 82,Войти в открывшийся проход
btn 38,Осмотреть письменный стол
btn 42,Осмотреть картину
btn 46,Выйти из кабинета
end

:82
pln Здесь находится тайная комната босса этой лаборатории. Сам он сидит на полу и что - то держит в руке.
btn 83,Посмотреть, что в руке
btn 37,Выйти из помещения
end

:83
if Антивирус then pln Нет, вам показалось - в руке у него ничего нет. & goto 82
pln В руке он держал шприц - пистолет с надписью "Антивирус Вероника".
inv+ Антивирус
goto 82
end

:46
if Антивирус then pln Вы помчались через толпу зомби к выходу, затем к аварийному лифту. & goto 84
if sara=0 and zomb5=1 then pln Какой смысл теперь идти туда - нужно спасаться. & goto 80
if zomb5=1 then pln Вы зашли в коридор, кишащий зомби. Теперь их стало намного меньше, но к вам всё равно тянуться руки десяти мертвецов. "Давай же, я их задержу" - кричит Сара.
pln Вы находитесь в каком-то коридоре. На одном его конце находятся две двери, а на другом- одна.
btn 47,Идти к двум дверям
btn 48,К одной двери
btn 37,В офис
end

:84
if open=0 then pln Высветилась надпись "Нужен доступ". & goto 80
if sara=1 then pln Пока вы поднимались на лифте, Сара вколола себе противоядие. & virus=0 & goto 85
pln Вы поднялись на лифте.
goto 85
end

:85
p Вы вышли из кабинки лифта и оказались на посадочной полосе, ограждённой трёхметровой решёткой под напряжением. Вы осмотрелись и увидели пески пустыни вокруг. Значит всё это время вы находились глубоко под землёй. Вот почему штаб - квартира этой компании так безнаказанно пустит ядерную ракету. Причём пустит её... Чёрт, через тридцать секунд !!! 
pln Вы быстро вскарабкались в вертолёт и не пристёгиваясь взлетели в воздух. Вы были в пятидесяти етрх от земли, когда ракета достигла своей цели.
if sara=0 then pln Вы могли спасти Сару... Но не сделали этого... & goto 86
if sara=1 and virus=1 then pln Внезапно кто - то коснулся вашего плеча. Это была Сара. Её кожа была серого цвета, она превратилась в зомби. Сара скопила остатки воли, достала автомат и застрелила себя... Вы потеряли Сару... & goto 87
pln Вы с Сарой отправились на заслуженный отдых после этого кошмара. & goto 88
end

:86
points=25
goto 89
end

:87
points=70
goto 89
end

:88
points=99
if boss>10 then points=100
goto 89
end

:89
music="1.ogg"
pln                    Поздравляю !!!
pln               Вы прошли игру "Вирус"
pln                  Ваш успех : #%points$%
pln Если эта игра вам понравилась, пишите на danil1@mailru.com,
pln        и заходите на мой сайт terracon.narod.ru
pln                      THE END
end

:48
if sara=1 then pln Вы помчались прямо в лапы зомби, где вас убили. & goto 8f
if zomb5=1 then pln Вы упёрлись спиной к запертой двери и достали своё верное оружие. & fight=5 & goto f5
if door2=0 then pln Дверь закрыта.
btn 46,Идти по коридору
end


:49
sara=1
pln Вы побежали по большому помещению. Человек в костюме, похоже не спешил что -либо объяснять. Вы остановили его, схватив за плечо.
pln - Ты кто такой ?
pln Человек снял защитную маску со своего лица. Это была девушка.
pln -Не мог догадаться, что - ли ? Ты понимаешь, что мы теряем драгоценное время ?
pln -Нет, я ничего не понимаю! Возможно я просто забыл... Я ничего не помню кроме своего имени.
pln -Чёрт! Как это случилось ? Ладно, давай я тебе вкратце объясню ситуацию. Меня зовут Сара. Ты - капитан охранной службы этого исследовательского комплекса, Рэй Джонс. Когда люди начали превращаться в зомби, весь твой отряд, состоящий из тридцати человек, в том числе и меня, начали зачистку помещений от этих тварей. Но их было слишком много и мы послали сигнал SOS нашей штаб - квартире. Нужно было улетать на вертолётах и мы направились в тот сектор. Но мы натолкнулись на двух тварей - раньше я видела их в стеклянных цистернах. Сражаясь с ними мы потеряли две третьих отряда - осталось только семь человек. Но мы убили их и направились дальше. К сожалению дверь лифта была закрыта, но мы нашли выжившего, который утверждал, что панель управления лифтом находится в секторе А, но ему нужна подмога, чтобы добраться туда. Пять человек отправились с ним. Остались только ты и я. Мы долго ждали из у лифта, но ни они не вернулись, ни лифт не открылся. Тогда мы разделились, чтобы быстрее найти наших ребят. Тут - то мы и встретились. Я никого не нашла, но обнаружила аварийный лифт в секторе А. Но для его активации тоже нужна карта. Такая карта есть у Джозефа, одного из тех пятерых. 
pln -Вынужден тебя огорчить, все эти парни мертвы, их убили растения - мутанты в ботаническом отделе.
pln -Всё очень печально. Похоже нам придётся направится туда, чтобы взять карточку от лифта. Лучше рискнуть, чем умереть в лапах зомби или от ядерной ракеты, которая скоро всё здесь уничтожит.
pln -Сара, есть ещё одна проблема. Третья тварь тоже выбралась из цистерны.
pln -Вдвоём нам с ней не справится. Будем надеяться, что мы просто не встретимся с ней.
btn 50,Бежать по туннелю
end


:50
plant=2
if zomb6=0 then pln Вы добежали до конца туннеля и вошли в дверь. Вы находитесь в ещё одном коридоре. К вам медленно идут три зомби. Сара уже открыла по ним огонь из автомата, дело за вами. & goto f6
pln Обыскав зомби, вы находите картридж для своего огнемёта, спрей первой помощи и обойму к пистолету.
inv+ 15,Патронов
inv+ 3,F.A.S.
inv+ 10,Топливо
btn 35,Идти в ботанический отдел
end

:47
if zomb5=1 then pln Вы помчались прямо навстречу толпе зомби, где вас сожрали. & goto 8f
pln На стене висит табличка "<= больница ;=>изолятор".
if zomb5=0 then pln Внезапно из правой двери на вас ринулся огромный поток зомби. Вы отбежали к другому концу коридора... Чёрт, дверь закрыта! Придётся отстреливаться! & zomb5=1 & goto 48
end

:42
if mon=1 then pln За картиной что-то щёлкнуло и в комнате открылась потайная дверь. & door1=1
pln Абстрактная картина - смесь треугольников, кругов, четырёхугольников. Есть разъём для руны прямоугольной формы.
if Руна then btn 43,Вставить руну
btn 37,Отойти от картины
end

:43
pln Вы вставили в специальный разъём руну.
mon=1
inv- Руна
goto 42
end

:41
if box1=1 then pln Ящик пуст. & goto 38
if not Маленький ключ then pln Ящик закрыт на замок. & goto 38
pln В ящике лежала обойма патронов, спрей первой помощи и чернильная лента.
pln Вы выбросили ненужный теперь маленький ключ.
inv+ Чернильная лента
inv+ 15,Патронов
inv+ F.A.S.
inv- Маленький ключ
box1=1
goto 38
end

:38
pln На письменном столе стоит старая печатная машинка, тут есть выдвижной ящик.
btn 39,Включить печатную машинку
btn 41,Открыть ящик
btn 37,Отойти от стола
end

:39
if not Чернильная лента then pln Для записи нужна чернильная лента! & goto 38
pln Вы действительно хотите записаться ?
btn 40,Да
btn 38,Нет
end

:40
inv- Чернильная лента
save 40
style_formpicture=55.jpg
music="1.ogg"
style_nonactivetext=Вирус
goto 38
end

:34
pln Здесь записано следующее послание
pln Раяна, пробирка с вирусом была разбита. Заразилось больше половины персонала -почти все учёные заразились и через неделю превратились в зомби .Мне не жаль этих ублюдков. Я остался незараженным, но пять человек, идущих со мной умерло из-за очередного творения этих мерзавцев. Они вырастили огромное растение, балуясь с генами. После того, как я разбил пробирку с вирусом, он добрался и до этого растения-гиганта. Если бы я знал, что вирус заражает не только класс животных, но и растения, я бы заблокировал тот отсек. Растение начало думать и испытывать голод не только в воде, но и в мясе. Оно захватило кого-то из персонала и сожрало его. Растение получило человеческую ДНК и мутировало ещё больше. Оно пустило почки на нижний этаж, в ботанический отсек. Из этих почек появились новые мутанты. Смесь растения с теплокровным, только вместо всех конечностей стебли-хлысты, а там, где должна быть голова - одна большая пасть с тонкими, но длинными и острыми зубами. Имеется скелет, а передвигается оно за счёт движения лже - корней. Мы пытались убить троих таких, но оказалось, что пули их не берут, потому как их раны мгновенно затягиваются. Помог огнемёт, однако ушла половина всего картриджа, пока я смог сжечь их всех. Все люди, что шли со мной погибли от ядовитых плевков этих тварей. Но, что сделано, то сделано и теперь я должен был убираться вместе с Ричардом. Но похоже, что он нас предал, Раяна. Судя по всему он хочет продать вирус властям. В любом случае я должен выбираться отсюда, пока ещё есть время.
pln Внизу написан электронный адрес, куда должно было быть направлено письмо. Убитый не успел лишь нажать кнопку "Отправить", до того, как его застрелили.
btn 30,Отойти от компьютера
end

:31
p Вы обыскали труп 
if not Огнемёт then p и нашли миниогнемёт & btn 32,Взять огнемёт
if bul1=0 then p и нашли обойму патронов & btn 33,Взять обойму
if Огнемёт and bul1=1 then p и не нашли ничего
pln .
btn 30,Отойти от трупа
end

:32
pln Вы взяли одноручный огнемёт и полупустой картридж с горючим к нему.
pln ВЫ ПОЛУЧИЛИ ОГНЕМЁТ.
inv+ Огнемёт
inv+ 5,Топливо
goto 31
end

:33

pln Вы взяли обойму к своему пистолету.
inv+ 15,Патронов
bul1=1
goto 31
end

:23
a=rnd*3
pln Вы открыли дневник на последней записи.
p Наш эксперимент вышел из-под контроля. Сегодня было объявлено, что одна из колб с вирусом была разбита. Нет, это не ошибка машины -кто-то намерено вытащил колбу с вирусом из хранилища и разбил её. Никто сюда не проник бы -значит кто-то из своих. Значит кто-то всё это время обманывал нас всех. Но выяснить, кто же это мне уже не удастся -меня, как и весь персонал поселили в изоляторе. Однако я сомневаюсь, что хоть кто-то из учёных и части охранников, находившихся в лаборатории не заразился -вирус распространяется мгновенно даже воздушным путём. Видимо поняв это, через два дня нас отпустили обратно в лабораторию. С этого дня весь незаражённый персонал ходил в специальных костюмах, не пропускающих воздух. Примерно через неделю я стал испытывать чесотку, слабую, но непрерывную головную боль. Уже через два дня я расчесал всю кожу на ногах и руках до крови, однако это уже не тревожило меня, так как головная боль стала просто нестерпимой. Любые попытки просто думать лишь усиливали головную боль. Пока я ещё в состоянии это сделать, я записываю код к складу лаборатории -
if a<1 then p 223 & otv=1 
if a<2 and a>1 then p 311 & otv=2
if a>2 then p 231 & otv=3
p Все мои бывшие коллеги превратились в зомби и начали нападать на незаражённый персонал. По нам тогда открыли огонь, но на зомби это никак не повлияло. В меня тогда попала пуля, но... я даже не почувствовал боли. Интересный фактор-зомби чуствуют наличие большого количества вируса в крови и поэтому знают, кто свой, а кто нет. Я тоже начинаю чувствовать что-то подобное. Я знаю - ещё час и я не выдержу, полностью лишусь разума. Лучше я покончу с собой, чем отдамся обречённой жизни.
btn 20,Закрыть дневник
end

:21
pln Похоже, что этот человек сам застрелил себя выстрелом из дробовика в упор. 
if key1=0 then pln В кармане охранника вы нашли ключ от подсобки. & inv+ Ключ от подсобки & key1=1
if not Дробовик then pln В руках он крепко зажал дробовик. & btn 22,Взять дробовик
btn 20,Отойти от трупа
end

:22
pln Вам пришлось попотеть, чтобы разжать руку покойника и взять дробовик.
inv+ Дробовик
inv+ 6,Дроби
pln ВЫ ПОЛУЧИЛИ ДРОБОВИК
goto 21
end

:15
if mon>0 then pln Монстр добрался до вас и разорвал на куски. & goto 8f
if pods=1 then goto 16
if not Ключ от подсобки then pln Дверь не открывается - она закрыта ключом. & goto 13
if Ключ от подсобки then pln Вы открыли дверь в подсобку. Теперь этот ключ вам больше не нужен и вы выбросили его. & pln ВЫ ВЫБРОСИЛИ КЛЮЧ ОТ ПОДСОБКИ. & pods=1 & & inv- Ключ от подсобки & goto 13
end

:16
p В этом помещении лежит куча хлама, но всё же есть и нужные вещи.
if med1=0 then p На полке вы видите флакончик с надписью "Спрей первой помощи" & btn 17,Взять спрей 
p На столике стоит старая печатная машинка. Как она могла здесь оказаться?
if ink1=0 then p На том же столике лежит чернильная лента для печатной машинке. & btn 18,Взять чернильную ленту.
btn 19,Включить печатную машинку
btn 13,Выйти из подсобного помещения
end

:19
if not Чернильная лента then pln Для сохранения нужна чернильная лента! & goto 16
pln Вы действительно хотите использовать чернильную ленту ?
btn 19.1,Да
btn 16,Нет
end

:19.1
pln Вы сохранили свою игру.
inv- Чернильная лента
save 19.1
instr style_formpicture=55.jpg
instr style_nonactivetext=Вирус
music="1.ogg"
goto 16
end

:17
pln Вы взяли спрей первой помощи.
inv+ F.A.S.
med1=1
goto 16
end

:18
pln Вы взяли чернильную ленту
inv+ Чернильная лента
ink1=1
goto 16
end

:Use_F.A.S._Использовать
pln Вы использовали спрей первой помощи.
inv- F.A.S.
inv+ 25,Здоровья
end

:14
pln " Выберите дверь, которую следует открыть/закрыть. "
if seca=0 then btn 14.1,Открыть сектор A
if seca=1 then btn 14.2,Закрыть сектор A
if secb=0 then btn 14.3,Открыть сектор B

if secb=1 then btn 14.4,Закрыть сектор B
if secc=0 then btn 14.5,Открыть сектор C
if secc=1 then btn 14.6,Закрыть сектор C
btn 13,Отойти от компьютера
end

:14.1
if not Карта A then pln "Требуется карта А."
if Карта A then pln "Сектор А открыт." & seca=0
goto 14
end

:14.2
if not Карта A then pln "Требуется карта А."
if Карта A then pln "Сектор А закрыт." & seca=1
goto 14
end

:14.3
if not Карта B then pln "Требуется карта B."
if Карта B then pln "Сектор B открыт." & secb=1
goto 14
end

:14.4
if not Карта B then pln "Требуется карта B."
if Карта B then pln "Сектор B закрыт." & secb=0
goto 14
end

:14.5
if not Карта C then pln "Требуется карта С."
if Карта C then pln "Сектор C открыт." & secc=1
goto 14
end

:14.6
if not Карта C then pln "Требуется карта С."
if Карта C then pln "Сектор C закрыт." & secc=0
goto 14
end

:2
if cor=3 then goto 4

:2.1
pln Внезапно из промежутков между неоновыми лампами появились лазерные лучи и образовали препятствие, которое резко понеслось в вашу сторону и разрубило вас пополам.
end

:f1
jizn=70
mest=4
mest2=4
fight=1
goto 1f
end

:f2
jizn=30
mest=0
mest2=2
fight=2
goto 1f
end

:f3
jizn=100

mest=4
mest2=3
fight=3
goto 1f
end

:f4
jizn=125
mest=4
mest2=5

fight=4
goto 1f
end

:f5
jizn=250
mest=0
mest2=13
fight=5
goto 1f
end

:f6
jizn=150
mest=4
mest2=6
fight=6
goto 1f
end

:f7
jizn=120
mest=4
mest2=5
fight=7
goto 1f
end

:f8
jizn=120
mest=1
mest2=4
fight=8
goto 1f
end

:f9
jizn=120
if boss>9 then jizn=60
mest=1
mest2=3
fight=9
goto 1f
end

:f10
jizn=200
mest=4
mest2=4
fight=10
goto 1f
end

:1f
if not Здоровья then goto 8f
if sara=1 then proc 13f
if jizn<1 then goto 6f
if fight=10 then pln Двухметровый мутант довольно быстро приближается к вам.
if fight=8 and jizn>90 then pln К вам идут четыре голодных зомби.
if fight=8 and jizn>60 and jizn<91 then pln К вам медленно идут три зомби.
if fight=8 and jizn>30 and jizn<61 then pln Два мертвеца жаждут вашей крови и идут к вам.
if fight=8 and jizn<31 then pln Один зомби приближается к вам, шаркая ногами.
if fight=7 and jizn>90 then pln К вам медленно сползаются четыре растения.
if fight=7 and jizn<91 and jizn>60 then pln Три растения медленно ползут к вам, шевеля своими хлыстами.
if fight=7 and jizn<61 and jizn>30 then pln К вам ползут два растения-мутанта.
if fight=7 and jizn<31 then pln К вам ползёт последнее растение-мутант.
if fight=6 and jizn>100 then pln К вам идут три зомби, издавая при этом невыносимые стоны.
if fight=9 and jizn>70 and jizn<91 then pln Квам идут три зомби.
if fight=9 and jizn>90 then pln Четыре зомби медленно идут к вам.
if fight=6 and jizn>49 and jizn<101 then pln К вам тянуться руки двух зомби.
if fight=6 and jizn<50 then pln Один мертвец бредёт к вам не обращая ни на что внимания.
if fight=5 then pln К вам движется поток живой мертвечины. Видимо в изоляторе находилось где-то полсотни зомби.
if fight=3 & not Спецкостюм then spr=spr+1
if fight=3 and spr>2 then pln Вы вдохнули и жгучие споры мгновенно попали в ваши лёгкие. Ещё немного и вы умрёте от удушья. & inv- 10,Здоровья
if fight=1 or fight=9 and jizn>35 and jizn<71 then pln Перед вами два зомби. Они голодны и медленно идут к вам.
if fight=1 or fight=9 and jizn<36 then pln К вам приближается голодный зомби.
if fight=4 and jizn<31 then pln Перед вами зомби. Он медленно идёт к вам.
if fight=4 and jizn>60 and jizn<96 then pln К вам приближаются три зомби.
if fight=4 and jizn>30 and jizn<61 then pln Перед вами два зомби. Они голодны и медленно идут к вам.
if fight=4 and jizn>95 then pln Перед вами четыре зомби. Они голодны и медленно идут к вам.
if fight=3 and jizn>34 and jizn<68 then pln Два мутировавших зомби идут к вам.
if fight=3 and jizn<35 then pln К вам приближается мутировавший зомби. Горловой мешок на его шее становится шире, он снова накапливает кислоту.
if fight=2 then pln Зомби жадно тянется к вашей ноге.
if fight=1 and jizn<36 then pln Один зомби медленно идёт к вам
mest2=mest2-1
if fight=10 then mest2=mest2-1
if fight=7 and not Бронежилет then pln Растение - мутант плюнуло в вас кислотой и оставило на вашем теле кровоточащие раны. & inv- 10,Здоровья
if fight=7 and mest2=1 and Бронежилет then pln Растение - мутант плюнуло в вас кислотой и она начала разъедать ваш бронежилет. & inv- 10,Бронежилет
if mest2<0 then mest2=1
if mest=0 then pln Вы упёрлись спиной к стенке. Дороги к отступлению уже нет!
if mest2=1 then pln Враг уже совсем близко!
if fight=3 and mest2=1 then a=rnd*2
if fight=3 and a<1 and a>0 then pln Зомби плюнул в вас кислотой, но промахнулся. & a=0
if fight=3 and a>1 and not Бронежилет then pln Зомби плюнул в вас кислотой, которая оставила на вашем теле кровоточащие раны. & inv- 10,Здоровья & a=0
if fight=3 and a>1 and Бронежилет then pln Зомби плюнул в вас кислотой, однако вас спас бронежилет. В некоторых местах кислота прожгла его насквозь. & inv- 10,Бронежилет & a=0
if not Патронов and not Дроби and not Топливо then pln Похоже, у вас закончились патроны. Придётся отбиваться ножом.
if fight<10 and mest2=0 and Бронежилет then inv- 20,Бронежилет & pln Эта тварь добралась до вас и теперь пытается разорвать ваш бронежилет в клочья! 
if fight<10 and mest2=0 and not Бронежилет then pln Эта тварь добралась до вас и начала вгрызаться в вашу сочную плоть. Чёрт, как больно! & inv- 15,Здоровья 
if fight=10 and mest2=0 then pln Мутант нанёс мощный рубящий удар своими когтями. & inv- 20,Здоровья
if Патронов к автомату then btn 14f,Обстрелять врага автоматной очередью
if mest2<4 and Топливо then btn 12f,Подпалить врага из огнемёта
if Дроби then btn 11f,Стрелять из дробовика
if Патронов then btn 3f,Стрелять во врага
if Патронов then btn 9f,Стрелять врагу в голову
if not Патронов and not Дроби and not Топливо then btn 10f,Пырнуть врага ножом
btn 5f,Оттолкнуть врага  
if mest>1 then btn 7f,Отойти назад
btn 1f,Ждать
end

:14f
pln Вы обстреляли врага из автомата.
inv- 5,Патронов к автомату
jizn=jizn-35
goto 1f
end

:13f
if fight=7 and sara=1 then pln Сара стреляет в растения из автомата, но это не даёт никаких результатов.
if fight<7 or fight>7 then pln Сара открыла огонь по зомби из своего M-16. & jizn=jizn-30
end

:12f
pln Вы подпалили врага со всех сторон. Начал слышаться треск из-за лопания его кожи.
inv- Топливо
jizn=jizn-30
goto 1f
end

:11f
if fight=7 then pln Вы стрельнули в растение из пистолета - никаких результатов - рана мгновенно зарослась. & inv- Патронов & goto 1f
pln Вы стрельнули в зомби из дробовика.
inv- Дроби
if mest2=0 then jizn=jizn-25 & goto 1f
if mest2=1 then jizn=jizn-20 & goto 1f
if mest2=2 then jizn=jizn-15 & goto 1f
if mest2>2 then jizn=jizn-10
goto 1f
end

:10f
p Вы подбежали и пырнули ножом во врага.
if fight=7 then pln К сожалению это ничего не дало. 
jizn=jizn-10
mest=mest+mest2
mest2=1
goto 1f
end

:9f
if fight=7 then pln Выстрелив, вы попали прямо в глотку растению. К сожалению это не дало никаких результатов. & inv- Патронов & goto 1f
a=rnd*4
if a<1 then pln Вы прицелились и мощным выстрелом раздробили зомби полголовы. & jizn=jizn-30
if a>1 then pln В панике вы стрельнули не целясь и пуля пролетела мимо.
inv- Патронов
goto 1f
end

:3f
if fight=7 then pln Вы стрельнули в растение, но рана моментально зарослась. & inv- Патронов & goto 1f
a=rnd*2
if a>1 or mest2<2 then pln Вы стрельнули в зомби и всадили пулю глубоко в его прогнившее тело. & jizn=jizn-15 & a=2
if a<=1 & mest2>1 then pln Вы стрельнули в зомби, но промахнулись.
inv- Патронов
goto 1f
end

:7f
pln Вы отошли назад.
mest=mest-2
mest2=mest2+2
goto 1f
end


:5f
if fight=2 then pln Вы ударили зомби с размаху по голове. Прогнившая плоть не выдержала удара и голова отлетела на пару метров, как  футбольный мяч. & jizn=jizn-30 & goto 1f
a=rnd*2
p Вы подбежали к зомби и пнули его ногой. Зомби отлетел на пару метров назад.
if a>1 then pln Но зомби успел сделать порез на вашей ноге своими когтями. & inv- 10,Здоровья
mest=mest+mest2
mest2=mest2+2
goto 1f
end

:8f
pln К сожаленью, вы мертвы.
end

:6f
if fight=1 then pln Теперь оба зомби смирно лежат на полу.
if fight=1 then zomb1=1 & goto 13
if fight=2 then pln Теперь эта тварь будет знать, что значит связаться с вами. & zomb2=1 & goto 20
if fight=3 then pln Мёртвые зомби начали превращаться в зелёную массу прямо у вас на глазах. Вскоре ничего, кроме следов пуль на стенах, не напоминало о том, что недавно здесь была схватка. & zomb3=1 & goto 30
if fight=4 then pln Вы уложили всех четырёх зомби. & zomb4=1 & goto 10
if fight=5 then pln Внезапно дверь за вами резко открылась. Не ожидав такого, вы операвшийся об эту дверь спиной, упали на пол, прямо под ноги зомби. Мертвецкие руки уже потянулись к вам, но вошедший человек (лица вы не увидели из-за спецкостюма) открыл по зомби огонь из своего M-16. Вы быстро встали на ноги и побежали в открывшуюся дверь. После этого ваш неожиданный спаситель прекращает стрельбу по зомби и бежит за вами. & goto 49
if fight=6 and mest2<2 then p Зомби рухнул у самых ваших ног.
if fight=6 then pln -Пойдём, у нас мало времени .- сказала Сара. Трое мертвецов лежали на полу, обстреленные со всех сторон. & zomb6=1 & goto 50
if fight=7 then pln Вы покончили со всеми растениями - мутантами. & zomb7=1 & goto 51
if fight=8 then pln Вы убили всех зомби. & zomb8=1 & goto 80
if fight=9 then pln Два зомби лежат на полу, иногда немного подёргиваясь. & zomb9=1 & goto 37 
if fight=10 then pln Вы сразили самое страшное исчадие этого кошмара. Поздравляю! Но кошмар ещё не закончен... & kill=1 & goto 77
end

